options {
	// force creation of instance-style parser
	STATIC = false;
}

PARSER_BEGIN(XPathTransformer)

// place the transformer in a meaningful package
package nl.utwente.cs.pxml.transform.xpath;

import java.io.StringReader;
import java.io.Reader;
import java.util.ArrayList;
import java.util.Arrays;
import java.util.List;

import nl.utwente.cs.pxml.util.CollectionUtils;

import com.beust.jcommander.JCommander;
import com.beust.jcommander.Parameter;
import com.beust.jcommander.ParameterException;

/**
 * XPathTransformer - JavaCC-generated parser to transform an XPath-expression on an a regular context to an 
 * XQuery-expression of that same document with a probabilistic overlay.
 */
class XPathTransformer {
	
	// store the currently supported axes for ease of use
	public static final List<String> SUPPORTED_AXES = Arrays.asList("child", "descendant");
	// store the axes participating in 'anchor' optimization
	public static final List<String> ANCHOR_AXES = Arrays.asList("child", "descendant-or-self", "descendant");

	// parameter to enable 'anchor' optimization
	@Parameter(names = "--anchor-opt", description = "use anchor optimization")
	protected boolean anchorOpt = false;

	// parameter to capture required arguments (required to be a list...)
	@Parameter(description = "<xpath-expression>")
	protected List<String> input;

	// helper variable to work around XQuery's immutable variables
	protected int currentStep = 0;

	// empty constructor (to be able to use it with JCommander)
	public XPathTransformer() {

	}

	// copied from generated code to be able to bypass constructor
	protected void init(Reader stream) {
	    jj_input_stream = new SimpleCharStream(stream, 1, 1);
	    token_source = new XPathTransformerTokenManager(jj_input_stream);
	    token = new Token();
	    jj_ntk = -1;
	    jj_gen = 0;
	    for (int i = 0; i < 2; i++) jj_la1[i] = -1;
    }

	/**
	 * Generates lines of XQuery code to perform a single step (in practice only supports descendant-style axes).
	 * 
	 * @param step
	 *         The XPath step to take.
	 */
	protected List<String> stepCode(String step) {
		List<String> expression = new ArrayList<String>();

		// increment step counter
		this.currentStep++;

		expression.add("");
		expression.add("(: take step " + this.currentStep + ": " + step + " :)");

		// create 'next' context
		expression.add(String.format("for $context%d in $context%d/%s",
				this.currentStep, this.currentStep - 1, step));

		// collect variable descriptors and combine
		expression.add(String.format("    let $descriptors%d := pxml:combine($descriptors%d, $context%d/ancestor-or-self::*/@p:descriptors/string())",
				this.currentStep, this.currentStep - 1, this.currentStep));

		// prune inconsistent possibilities
		expression.add(String.format("    where pxml:consistent($descriptors%d)", this.currentStep));
		expression.add("return");

		return expression;
	}

	public static void main(String... args) {
		// create a new transformer
		XPathTransformer transformer = new XPathTransformer();
		JCommander arguments = new JCommander(transformer);

		// parse the input string, the result is a string containing the resulting XQuery-expression
		try {
			// have JCommander parse and assign arguments
			arguments.parse(args);
			// force the transformer to (re)init using the captured input
			transformer.init(new StringReader(transformer.input.get(0)));

			// compile input to output
			String code = transformer.parseInput();

			// print header
			System.out.println("(: transforming XPath expression " + transformer.input.get(0) + " :)\n");

			// print the actual generated code
			System.out.println(code);
		} catch (ParameterException e) {
		e.printStackTrace();
			arguments.usage();
			System.exit(1);
		} catch (ParseException e) {
			System.err.println(e.getMessage());
			System.exit(1);
		} catch (TokenMgrError e) {
			System.err.println(e.getMessage());
			System.exit(1);
		}
	}

	/**
	 * Simple record class combining an axis and a node test.
	 */
	public class AxisStep {
		
		// the axis this step takes
		public final String axis;
		// the node test along the axis
		public final String nodeTest;
		
		public AxisStep(String axis, String nodeTest) {
			this.axis = axis;
			this.nodeTest = nodeTest;
		}
		
		/**
		 * Prints a verbose axis step in the form
		 *     axis::nodeTest
		 */
		@Override
		public String toString() {
			return this.axis + "::" + this.nodeTest;
		}
	
	}
	
}

PARSER_END(XPathTransformer)

TOKEN : {
// string constant (document name)
  < STRING : ("\""(["A"-"z", "0"-"9", ".", "_", "-"])*"\"") >

// XPath step character
| < STEP : "/" >

// XPath axis names (copied from http://www.w3.org/TR/xpath/ section 2.2)
| < AXIS : 
	  "ancestor"
	| "ancestor-or-self"
	| "attribute"
	| "child"
	| "descendant"
	| "descendant-or-self"
	| "following"
	| "following-sibling"
	| "namespace"
	| "parent"
	| "preceding"
	| "preceding-sibling"
	| "self" >

// separator between axis name and node test
| < SEPARATOR : "::" >

// node names, required to start with a letter or _, followed by a word character (A-z, 0-9 or _)
| < NODENAME : (["A"-"z", "_"])(["0"-"9", "A"-"z", "_"])* >

// XPath expression for 'any'
| < NODEANY : "*" >

// XPath node types (copied from http://www.w3.org/TR/xpath/ section 3.7)
| < NODETYPE : 
	  "comment()"	
	| "text()"	
	| "processing-instruction()"	
	| "node()" >
}

String parseInput() :
{
	String docName;
	AxisStep axisStep;
	boolean onDescendantAxis = false;
	List<String> anchorBacklog = new ArrayList<String>();
	List<String> expression = new ArrayList<String>();
}
{
	// start with a document name match
	docName = document()
	{
		// declare a namespace for the answers
		expression.add("declare namespace p = \"http://www.cs.utwente.nl/~keulen/pxml\";");
		// import PXML query module
		expression.add("import module namespace pxml = \"java:nl.utwente.cs.pxml.PXML\";");

		// declare some variables to be used
		expression.add("");
		// start of XQuery content (collect answers in result node)
		expression.add("<p:result>{");
		expression.add("");
		expression.add("let $context0 := doc(" + docName + ")"); // quotes are included in the document name
		expression.add("let $descriptors0 := \"\"");
	}

	// proceed with one or more axis steps
	(
		axisStep = axisStep()
		{
			// check if the axis is supported
			if (!SUPPORTED_AXES.contains(axisStep.axis)) {
				// TODO: throw more meaningful exception
				throw new RuntimeException("axis " + axisStep.axis + " is not supported");
			}

			if (this.anchorOpt && ANCHOR_AXES.contains(axisStep.axis)) {
				// optimize descendant axis if requested
				anchorBacklog.add(axisStep.toString());
			} else {
				// if backlog is not empty and we got to this point, clear backlog now before appending current step
				if (!anchorBacklog.isEmpty()) {
					expression.addAll(this.stepCode(CollectionUtils.join(anchorBacklog, "/")));
					anchorBacklog.clear();
				}

				// append code for current step
				expression.addAll(this.stepCode(axisStep.toString()));
			}
		}
	)+
	{
		// first check to see if backlog is clear
		if (!anchorBacklog.isEmpty()) {
			expression.addAll(this.stepCode(CollectionUtils.join(anchorBacklog, "/")));
			anchorBacklog.clear();
		}

		// prepare result variables
		expression.add("");
		expression.add("(: all steps taken, calculate probabilities for all resulting nodes :)");
		expression.add("let $answer := $context" + this.currentStep);
		expression.add("let $descriptors := $descriptors" + this.currentStep);
		expression.add("let $probability := pxml:probability($context0//p:variables, $descriptors)");

		// output result nodes
		expression.add("");
		expression.add("return <p:answer p:descriptors=\"{$descriptors}\" p:probability=\"{$probability}\">{$answer}</p:answer>");

		// end of XQuery expression, close scope and result node
		expression.add("");
		expression.add("}</p:result>");

		// return final expression as a single string
		return CollectionUtils.join(expression, "\n");
	}
}

/**
 * Matches a document reference of the form
 *     doc("docName.xml")
 * and returns the string literal (including the quotes).
 */
String document() :
{
	// use local variable docName to store document name token
	Token docName;
}
{
	"doc(" docName = < STRING > ")"
	{
		// return the string constant (including quotes)
		return docName.image;
	}
}

/**
 * Matches a single XPath axis step of the form
 *     /axis::nodeTest
 * and returns it as it was matched.
 */
AxisStep axisStep() :
{
	// use local variable axis to store the axis (initialized at null so javac won't complain about initialization)
	Token axis = null;
	// use local variable nodeTest to store the nodeTest on the axis
	Token nodeTest = null;
}
{
	< STEP >
	axis = < AXIS >
	< SEPARATOR >
	( nodeTest = < NODENAME > | < NODEANY > | < NODETYPE > )
	{
		// return the input, an XPath axis step
		return new AxisStep(axis.image, nodeTest.image);
	}
}
